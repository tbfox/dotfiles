#!/usr/bin/env nu

def main [] {
    help main
}

def 'main list caps' [] {
    get-folder-contents capabilities 
}

def 'main list prac' [] {
    get-folder-contents practices
}

def 'main list res people' [] {
    get-folder-contents resources/people
}

def 'main list res process' [] {
    get-folder-contents resources/process
}

def 'main list res tech' [] {
    get-folder-contents resources/tech
}

# def 'main parse' [] {
#     get-folder-contents capabilities 
#     | where title == "Job Satisfaction"
#     | get path
#     | get 0
#     | open 
#     | lines
#     | where {|line| ($line | str trim) != ""}
#     | str join "\n"
#     | split row "\n## "
#     | each { 
#         let content = split row "\n### ";
#         let head = $content | take 1;
#         {
#             heading: ($head | get 0)
#             items: $content
#         }
#     }
# }

def 'main new practice' [--dry-run(-d)] {
    print "What is the name of your action oriented practice?"
    let name = (input "> ")
    print "Supported capabilities: "
    let selected_capability_titles = (main list caps | get title | input list -m)
    let selected_capabilities = main list caps | where {|x| ($x | get title) in $selected_capability_titles }

    let path = [$env.OPEN_PRACTICE_REPO capabilities $"($name | str kebab-case).md"] | path join
    
    let content = $"# ($name | str title-case )

<!-- Quick 2-4 sentence summary. What’s the practice? Why should teams care? Keep it casual and motivating. -->

## When to Experiment

<!-- You are a [persona] and need to [learn how to / ensure that] so you can [end goal].”
\(List for each relevant persona: Non-technical exec, Technical exec, Developer, QA, PM, Product Manager, etc.) -->

## How to Gain Traction

<!-- List 3–5 steps to take a team from zero to adopted.
For each step, include:
  ### [Action Step]
  3 sentences on how to do it, how to get buy-in, and what tools/resources help. Any external resources \(videos, guides, book lists, templates, etc.) that help a team adopt this practice should be linked here within the relevant action step. -->

## Lessons From The Field

<!-- This section captures real-world patterns \(things that consistently help or hinder this practice) along with short, relevant stories from the field. It’s not for personal rants or generic opinions. Each entry must be based on either:
1. a repeated observation across teams, or
2. a specific example \(what worked, what didn’t, and why). -->

## Deciding to Polish or Pitch

After experimenting with this practice for [**insert appropriate quantity of time in bold**], bring the team together to determine whether the following metrics and/or signals have changed in a positive direction:

<!-- 
List target metrics, qualitative markers, or lightweight feedback mechanisms. Review this article for inspiration: https://dora.dev/research/2025/measurement-frameworks/

Organize metrics and signals into Fast & Measurable, Fast & Intangible, Slow & Measurable, or Slow & Intangible, but only include categories with strong, defensible signals. Exclude weak or hard-to-attribute signals.

For measurable items, specify how to track them \(e.g., DX, Jira, CI dashboards). For intangible items, note how to capture feedback \(e.g., surveys, retro notes, developer chatter).

Keep metrics scoped and outcome-focused \(e.g., “reduced lead time for cross-repo changes” instead of just “reduced lead time”). -->

## Supporting Capabilities

($selected_capabilities | each {|cap| get-cap-link $cap.title $cap.path} | str join "\n\n"))

"
    if $dry_run {
        print $content
    } else {
        $content | save $path
    }
}

def get-cap-link [title path] {
    $"# [($title)]\(/capabilities/($path | path parse | get stem).md)\n\n<!-- 1–2 sentences on how this practice helps improves this capability -->."
}

def get-repo-path [] {
    try {
        $env.OPEN_PRACTICE_REPO
    } catch {
        print $"(ansi red)Error:(ansi reset) Environment variable OPEN_PRACTICE_REPO not found.

Please define the OPEN_PRACTICE_REPO environment variable as the path where you
have the open practice repo downloaded. If you don't have the open practice
repository downloaded you can get it here:

    git clone https://github.com/pragmint/open-practices
"
    }
}

def get-folder-contents [folder] {
    ls ([(get-repo-path) $folder] | path join)
    | get name
    | each {|path|
        {
            title: ($path | path parse | get stem | format title)
            path: $path
        }
    }
}


def 'format title' [] {
    $in | split row "-"
    | each {|word| 
        if ($word in [in the of to]) {
            $word 
        } else { 
            $word | str capitalize 
        }
    } 
    | str join " "
}



